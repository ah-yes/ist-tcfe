options savecurrents

**envelope detector

*diode
D1 1 3 modelo
.MODEL modelo D
D2 2 3 modelo
D3 GND 2 modelo
D4 GND 1 modelo

*resistor
R1 3 GND 10.0k

*capacitor
C1 3 GND 10u

**voltage regulator

*resistor2
R2 3 4 19k

*diodes
D5 4 5 modelo
D6 5 6 modelo
D7 6 7 modelo
D8 7 8 modelo
D9 8 9 modelo
D10 9 12 modelo
D11 12 13 modelo
D12 13 14 modelo
D13 14 15 modelo
D14 15 16 modelo
D15 16 17 modelo
D16 17 18 modelo
D17 18 GND modelo

* supply voltage
Vcc 10 GND 0 ac 1 SIN(0 230 50 0 0 90)

**tranformador

V1 1 11 0
F2 10 GND E1 2.7664k
E1 11 2 (10,GND) 2.7664k

.control

op

set hcopypscolor=0
set color0=white
set color1=black
set color2=red
set color3=blue
set color4=violet
set color5=rgb:3/8/0
set color6=rgb:4/0/0

*transient analysis
tran 15e-5 200e-3

meas tran DCLEVEL AVG v(4) from=0m to=100e-3
meas tran Maximum  MAX v(4) from=0m to=100e-3
meas tran Minimum MIN v(4) from=0m to=100e-3

hardcopy trans.ps v(4)
echo trans_FIG
hardcopy trans2.ps v(3)
echo trans2_FIG
hardcopy trans4.ps 12-v(4)
echo trans4_FIG


echo " "
echo "********************************************"
echo  "Operating point"
echo "********************************************"
echo  "op_TAB"

print DCLEVEL
print Maximum-Minimum
*print Maximo
*print Minimo

echo  "op_END"



hardcopy trans3.ps v(10) v(1)-v(2)
echo trans3_FIG 
quit
	 
.endc
.end
