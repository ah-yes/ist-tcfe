*-----------------------------------------------------------------------------
*
* To use a subcircuit, the name must begin with 'X'.  For example:
* X1 1 2 3 4 5 uA741
*
* connections:   non-inverting input
*                |  inverting input
*                |  |  positive power supply
*                |  |  |  negative power supply
*                |  |  |  |  output
*                |  |  |  |  |
.subckt uA741    1  2  3  4  5
*
  c1   11 12 8.661E-12
  c2    6  7 30.00E-12
  dc    5 53 dx
  de   54  5 dx
  dlp  90 91 dx
  dln  92 90 dx
  dp    4  3 dx
  egnd 99  0 poly(2) (3,0) (4,0) 0 .5 .5
  fb    7 99 poly(5) vb vc ve vlp vln 0 10.61E6 -10E6 10E6 10E6 -10E6
  ga    6  0 11 12 188.5E-6
  gcm   0  6 10 99 5.961E-9
  iee  10  4 dc 15.16E-6
  hlim 90  0 vlim 1K
  q1   11  2 13 qx
  q2   12  1 14 qx
  r2    6  9 100.0E3
  rc1   3 11 5.305E3
  rc2   3 12 5.305E3
  re1  13 10 1.836E3
  re2  14 10 1.836E3
  ree  10 99 13.19E6
  ro1   8  5 50
  ro2   7 99 100
  rp    3  4 18.16E3
  vb    9  0 dc 0
  vc    3 53 dc 1
  ve   54  4 dc 1
  vlim  7  8 dc 0
  vlp  91  0 dc 40
  vln   0 92 dc 40
.model dx D(Is=800.0E-18 Rs=1)
.model qx NPN(Is=800.0E-18 Bf=93.75)
.ends



.options savecurrents

Vcc vcc 0 5.0
Vee vee 0 -5.0
Vin in 0 0 ac 1 sin(0 10m 1k)

X1 ninv_in inv_in vcc vee out uA741

R2 out out2 1k
C2 out2 0 110n



R4 out inv_in 100k
R3 inv_in 0 1k

R1 ninv_in 0 1k
C1 ninv_in in 220n

.op
.end

.control

set filetype=ascii
run


set hcopypscolor=0
set color0=white
set color1=black
set color2=red
set color3=blue
set color4=violet
set color5=rgb:3/8/0
set color6=rgb:4/0/0

op



print all

* time analysis
tran 1e-5 1e-2
plot v(out)
hardcopy sanitycheck3.ps v(in)

* frequency analysis
ac dec 10 1 100MEG
plot vdb(out)
plot vp(out)
hardcopy vo1f.ps vdb(out2)
echo vo1f_FIG
hardcopy sanitycheck.ps v(in)
hardcopy sanitycheck2.ps v(out2)


*Central Frequency
meas ac ymax MAX vdb(out2) from=0 to=100MEG
let x='ymax-3'
meas ac xeval1 WHEN vdb(out2)=x
meas ac xeval2 WHEN vdb(out2)=x Fall=LAST



let f='sqrt((xeval1*xeval2))'
print f
meas ac Gain Find vdb(out2) AT=f
meas ac yes2 Find vin#branch AT=f
let z='abs(1/yes2)'

echo  "op_TAB"
print f
print Gain
print z
echo  "op_END"

*input impedance in kohm
print v(in)[40]/R1[i]/1000
hardcopy inputimpedance.ps abs(v(in)/vin#branch)
echo inputimpedance_FIG
*need a different setup to measure output impedance

.endc 

